//------------------------------------------------------------------

#property copyright "mladen"
#property link      "www.forex-tsd.com"

//------------------------------------------------------------------

#property indicator_chart_window
#property indicator_buffers 2
#property indicator_plots   1

#property indicator_label1  "Hull"
#property indicator_type1   DRAW_COLOR_LINE
#property indicator_color1  clrLimeGreen,clrPaleVioletRed
#property indicator_style1  STYLE_SOLID
#property indicator_width1  2

//
//
//
//
//

enum enPrices
  {
   pr_close,      // Close
   pr_open,       // Open
   pr_high,       // High
   pr_low,        // Low
   pr_median,     // Median
   pr_typical,    // Typical
   pr_weighted,   // Weighted
   pr_average,    // Average (high+low+oprn+close)/4
   pr_haclose,    // Heiken ashi close
   pr_haopen,     // Heiken ashi open
   pr_hahigh,     // Heiken ashi high
   pr_halow,      // Heiken ashi low
   pr_hamedian,   // Heiken ashi median
   pr_hatypical,  // Heiken ashi typical
   pr_haweighted, // Heiken ashi weighted
   pr_haaverage   // Heiken ashi average
  };

//
//
//
//
//

input int       HullLength  = 27;       // Hull MA calculaion period
input enPrices  Price       = pr_close; // Price to use
input double    HullDivisor = 1.5;      // Hull calculation "speed"

//
//
//
//
//

double hull[];
double colorBuffer[];

//------------------------------------------------------------------
//
//------------------------------------------------------------------
//
//
//
//
//

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
int OnInit()
  {
   SetIndexBuffer(0,hull,INDICATOR_DATA);
   SetIndexBuffer(1,colorBuffer,INDICATOR_COLOR_INDEX);

//
//
//
//
//

   IndicatorSetString(INDICATOR_SHORTNAME," Hull trend ("+string(HullLength)+")");
   return(0);
  }

//------------------------------------------------------------------
//
//------------------------------------------------------------------
//
//
//
//
//

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,
                const int prev_calculated,
                const datetime& time[],
                const double& open[],
                const double& high[],
                const double& low[],
                const double& close[],
                const long& tick_volume[],
                const long& volume[],
                const int& spread[])
  {
//
//
//
//
//

   for(int i=(int)MathMax(prev_calculated-1,0); i<rates_total; i++)
     {
      hull[i] = iHull(getPrice(Price,open,close,high,low,i,rates_total),HullLength,HullDivisor,i,rates_total,0);
      if(i>0)
        {
         colorBuffer[i] = colorBuffer[i-1];
         if(hull[i] > hull[i-1])
            colorBuffer[i]= 0;
         if(hull[i] < hull[i-1])
            colorBuffer[i]= 1;
        }
     }
   return(rates_total);
  }



//------------------------------------------------------------------
//
//------------------------------------------------------------------
//
//
//
//
//


double workHa[][4];
double getPrice(enPrices price, const double& open[], const double& close[], const double& high[], const double& low[], int i, int bars)
  {
   if(price>=pr_haclose && price<=pr_haaverage)
     {
      if(ArrayRange(workHa,0)!= bars)
         ArrayResize(workHa,bars);

      //
      //
      //
      //
      //

      double haOpen;
      if(i>0)
         haOpen  = (workHa[i-1][2] + workHa[i-1][3])/2.0;
      else
         haOpen  = open[i]+close[i];
      double haClose = (open[i] + high[i] + low[i] + close[i]) / 4.0;
      double haHigh  = MathMax(high[i], MathMax(haOpen,haClose));
      double haLow   = MathMin(low[i], MathMin(haOpen,haClose));

      if(haOpen  <haClose)
        {
         workHa[i][0] = haLow;
         workHa[i][1] = haHigh;
        }
      else
        {
         workHa[i][0] = haHigh;
         workHa[i][1] = haLow;
        }
      workHa[i][2] = haOpen;
      workHa[i][3] = haClose;
      //
      //
      //
      //
      //

      switch(price)
        {
         case pr_haclose:
            return(haClose);
         case pr_haopen:
            return(haOpen);
         case pr_hahigh:
            return(haHigh);
         case pr_halow:
            return(haLow);
         case pr_hamedian:
            return((haHigh+haLow)/2.0);
         case pr_hatypical:
            return((haHigh+haLow+haClose)/3.0);
         case pr_haweighted:
            return((haHigh+haLow+haClose+haClose)/4.0);
         case pr_haaverage:
            return((haHigh+haLow+haClose+haOpen)/4.0);
        }
     }

//
//
//
//
//

   switch(price)
     {
      case pr_close:
         return(close[i]);
      case pr_open:
         return(open[i]);
      case pr_high:
         return(high[i]);
      case pr_low:
         return(low[i]);
      case pr_median:
         return((high[i]+low[i])/2.0);
      case pr_typical:
         return((high[i]+low[i]+close[i])/3.0);
      case pr_weighted:
         return((high[i]+low[i]+close[i]+close[i])/4.0);
      case pr_average:
         return((high[i]+low[i]+close[i]+open[i])/4.0);
     }
   return(0);
  }



//------------------------------------------------------------------
//
//------------------------------------------------------------------
//
//
//
//
//

double workHull[][2];
double iHull(double price, double period, double divisor, int r, int total, int instanceNo=0)
  {
   if(ArrayRange(workHull,0)!= total)
      ArrayResize(workHull,total);

//
//
//
//
//

   int HmaPeriod  = (int)MathMax(period,2);
   int HalfPeriod = (int)MathFloor(HmaPeriod/divisor);
   int HullPeriod = (int)MathFloor(MathSqrt(HmaPeriod));
   double weight;
   instanceNo *= 2;

   workHull[r][instanceNo] = price;

//
//
//
//
//

   double hmw = HalfPeriod;
   double hma = hmw*price;
   for(int k=1; k<HalfPeriod && (r-k)>=0; k++)
     {
      weight = HalfPeriod-k;
      hmw   += weight;
      hma   += weight*workHull[r-k][instanceNo];
     }
   workHull[r][instanceNo+1] = 2.0*hma/hmw;

   hmw = HmaPeriod;
   hma = hmw*price;
   for(int k=1; k<period && (r-k)>=0; k++)
     {
      weight = HmaPeriod-k;
      hmw   += weight;
      hma   += weight*workHull[r-k][instanceNo];
     }
   workHull[r][instanceNo+1] -= hma/hmw;

//
//
//
//
//

   hmw = HullPeriod;
   hma = hmw*workHull[r][instanceNo+1];
   for(int k=1; k<HullPeriod && (r-k)>=0; k++)
     {
      weight = HullPeriod-k;
      hmw   += weight;
      hma   += weight*workHull[r-k][1+instanceNo];
     }
   return(hma/hmw);
  }
//+------------------------------------------------------------------+
